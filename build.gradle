buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
		maven { url = "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}

apply from: "https://gradle-files.u-team.info/default-project.gradle"

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "maven"

repositories {
    mavenCentral()
}

group = "info.u-team"
archivesBaseName = generateArchivesBaseName()
version = generateVersion()

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    mappings channel: config.forge.mapping_channel, version: config.forge.mapping
    runs {
    	client = {
            workingDirectory project.file("run/client/default").canonicalPath
			mods {
            	"${config.mod.modid}" {
                    source sourceSets.main
				}
            }
        }
        client_first = {
       		parents runs.getByName("client")
            workingDirectory project.file("run/client/one").canonicalPath
            args "--username HyCraftHD"
        }
        client_second = {
       	 	parents runs.getByName("client")
       	 	args "--username UTeam"
            workingDirectory project.file("run/client/second").canonicalPath
        }
        server = {
            workingDirectory project.file("run/server").canonicalPath
            mods {
                "${config.mod.modid}" {
                    source sourceSets.main
				}
			}
        }
        data = {
            workingDirectory project.file("run/data").canonicalPath
            args "--mod", config.mod.modid, "--all", "--output", "\"" + file("src/generated/resources").toString() + "\"", "--validate", "--existing", "\"" + sourceSets.main.resources.srcDirs[0] + "\""
            mods {
                "${config.mod.modid}" {
                    source sourceSets.main
				}
			}
        }
    }
}

configurations {
    internal
    compile.extendsFrom(internal)
}

dependencies {
    minecraft getForgeDependency()
	compile getUTeamCoreDependency()
	compile project(":audio-client")
  	compile ("info.u-team:music_player-${config.forge.mcversion}:${config.musicplayer.version}:dev")
	internal ("org.lwjgl:lwjgl-opus:3.2.2") {
		transitive=false
	}
	internal ("org.lwjgl:lwjgl-opus:3.2.2:natives-linux") {
		transitive=false
	}
	internal ("org.lwjgl:lwjgl-opus:3.2.2:natives-macos") {
		transitive=false
	}
	internal ("org.lwjgl:lwjgl-opus:3.2.2:natives-windows") {
		transitive=false
	}
}

jar {
	classifier = ""

    from "LICENSE"
    
    jar.dependsOn ":opus-java:build"
    jar.dependsOn ":audio-client:build"

	from "opus-java/build/classes/java/main"
    from "opus-java/CONCENTUS-LICENSE"
     
    from "audio-client/build/classes/java/main"
                  
    into ("META-INF/libraries") {
		from {
			configurations.internal.incoming.artifacts.getArtifacts().stream().map { resolvedArtifact ->
				def file = resolvedArtifact.getFile()
				def fileName = resolvedArtifact.getId().getFileName()
				def artifact = resolvedArtifact.getId().getComponentIdentifier().getDisplayName()
				def classifier = resolvedArtifact.getId().getName().getClassifier()
				if(classifier != null) {
					artifact += ":" + classifier
				}

				def metaFile = new File(jar.getTemporaryDir(), fileName + ".meta")
				metaFile.text = "Maven-Artifact: " + artifact
				
				return [file, metaFile]
			}.collect()
		}
	}
                  
    exclude(".cache")      
           
   	manifest normalManifest
   	manifest {
   		attributes "ContainedDeps": "Test.jar"
   	}
}

task deobfJar(type: Jar) {
	classifier = "dev"

    from sourceSets.main.output
    from "LICENSE"
    
    jar.dependsOn ":opus-java:build"
    jar.dependsOn ":audio-client:build"

	from "opus-java/build/classes/java/main"
    from "opus-java/CONCENTUS-LICENSE"
     
    from "audio-client/build/classes/java/main"
        
    exclude(".cache")    
            
    manifest normalManifest
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    
    from sourceSets.main.allJava
    from "LICENSE"
}

task docJar(type: Jar) {
    classifier = "javadoc"
    
    from javadoc
    from "LICENSE"
    
    tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
		options.addStringOption('encoding', 'UTF-8')
		options.addStringOption('charSet', 'UTF-8')
	}
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives docJar
    archives jar
}

signAllJars()

sourceSets {
	main {
		java {
			srcDirs += "src/audio/java"
			srcDirs += "src/musicplayer-integration/java"
		}
		resources {
			srcDirs += "src/generated/resources"
		}
	}
}

curseforge {
	afterEvaluate {
		tasks."curseforge${config.curse.id}".onlyIf {
			getValue("token") != null
		}
	}
	
	tasks.curseforge.dependsOn build
	
	apiKey = getValue("token")
	
  	project {
    	id = config.curse.id
    	releaseType = config.curse.type

		addGameVersion config.forge.mcversion
		
		options {
		    forgeGradleIntegration = false
		}
		
		relations {
      		requiredDependency "u-team-core"
			optionalDependency "music-player"
    	}

    	mainArtifact(jar) {
      		displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}"
      		changelogType = "html"
      		changelog = "<a href=\"" + generateChangelogUrl() + "\">Changelog of ${project.version}</a>"
    	}
    	
    	addArtifact(deobfJar) {
    		displayName = "${config.curse.display}-${config.forge.mcversion}-${project.version}-dev"
    		changelogType = "text"
    		changelog = "Mappings: ${config.forge.mapping_channel}_${config.forge.mapping}"
    	}
  	}
}

uploadArchives {
	onlyIf {
		getValue("mavenpass") != null
	}

	tasks.uploadArchives.dependsOn build

	repositories {
		mavenDeployer {
			repository(url: "https://repo.u-team.info") {
				authentication(userName: "maven", password: getValue("mavenpass"))
			}
			pom {
				groupId = project.group
				artifactId = project.archivesBaseName
				version = project.version
				project {
					name project.archivesBaseName
					packaging "jar"
					description "${config.github.name}"
					url "https://github.com/MC-U-Team/${config.github.name}"
					scm {
						url "https://github.com/MC-U-Team/${config.github.name}"
						connection "scm:git:git://github.com/MC-U-Team/${config.github.name}.git"
						developerConnection "scm:git:git@github.com:MC-U-Team/${config.github.name}.git"
					}
					issueManagement {
						system "github"
						url "https://github.com/MC-U-Team/${config.github.name}/issues"
					}
					licenses {
						license {
							name "Apache License 2.0"
							url "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/LICENSE"
							distribution "repo"
						}
					}

					developers {
						developer {
							name "HyCraftHD"
						}
					}
				}
				setConfigurations(null) // Prevent all dependencies in the pom file
			}
		}
	}
}

task uploadBoth {
	tasks.uploadBoth.dependsOn build
	tasks.uploadBoth.dependsOn uploadArchives
	tasks.uploadBoth.dependsOn "curseforge${config.curse.id}"
	tasks.uploadBoth.dependsOn setBuildNumber
	doLast {
		println "Published both"
	}
}

gradle.taskGraph.whenReady {
	if (gradle.taskGraph.hasTask(":curseforge${config.curse.id}") || gradle.taskGraph.hasTask(":uploadArchives")) {
		validateBuild()
	}
}